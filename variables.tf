variable "resolver" {
  description = "contains all private dns resolver configuration"
  type = object({
    name                = string
    resource_group_name = optional(string)
    location            = optional(string)
    virtual_network_id  = string
    tags                = optional(map(string))
    inbound_endpoints = optional(map(object({
      name = optional(string)
      ip_configurations = optional(map(object({
        private_ip_allocation_method = optional(string, "Dynamic")
        private_ip_address           = optional(string)
        subnet_id                    = string
      })), {})
    })), {})
    outbound_endpoints = optional(map(object({
      name      = optional(string)
      subnet_id = string
      forwarding_rulesets = optional(map(object({
        name = optional(string)
        tags = optional(map(string))
        rules = optional(map(object({
          name        = optional(string)
          domain_name = string
          enabled     = optional(bool)
          metadata    = optional(map(string))
          target_dns_servers = map(object({
            ip_address = string
            port       = optional(number, 53)
          }))
        })), {})
        virtual_network_links = optional(map(object({
          name               = optional(string)
          metadata           = optional(map(string))
          virtual_network_id = string
        })), {})
      })), {})
    })), {})
  })

  validation {
    condition     = var.resolver.location != null || var.location != null
    error_message = "location must be provided either in the resolver object or as a separate variable."
  }

  validation {
    condition     = var.resolver.resource_group_name != null || var.resource_group_name != null
    error_message = "resource group name must be provided either in the resolver object or as a separate variable."
  }
}

variable "location" {
  description = "default azure region to be used."
  type        = string
  default     = null
}

variable "resource_group_name" {
  description = "default resource group to be used."
  type        = string
  default     = null
}

variable "tags" {
  description = "tags to be added to the resources"
  type        = map(string)
  default     = {}
}
